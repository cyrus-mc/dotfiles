# shortcut to this dotfiles path is $ZSH
export ZSH=$HOME/.dotfiles
export ZSH_CACHE_DIR=$ZSH/cache

# your project folder that we can `c [tab]` to
export PROJECTS=~/code

# Stash your environment variables in ~/.localrc. This means they'll stay out
# of your main dotfiles repository (which may be public, like this one), but
# you'll have access to them in your scripts.
if [[ -a ~/.localrc ]]
then
  source ~/.localrc
fi

# all of our zsh files
typeset -U config_files
config_files=($ZSH/**/*.zsh)

# load the path files
for file in ${(M)config_files:#*/path.zsh}
do
  source $file
done

# load everything but the path and completion files
for file in ${${config_files:#*/path.zsh}:#*/completion.zsh}
do
  source $file
done

# initialize autocomplete here, otherwise functions won't be loaded
autoload -U compinit
compinit

# load every completion after autocomplete loads
for file in ${(M)config_files:#*/completion.zsh}
do
  source $file
done

unset config_files

# add Pulumi to the PATH
export PATH=$PATH:$HOME/.pulumi/bin

# tabtab source for serverless package
# uninstall by removing these lines or running `tabtab uninstall serverless`
[[ -f /Users/matthewce/tmp/bb-helper/node_modules/tabtab/.completions/serverless.zsh ]] && . /Users/matthewce/tmp/bb-helper/node_modules/tabtab/.completions/serverless.zsh
# tabtab source for sls package
# uninstall by removing these lines or running `tabtab uninstall sls`
[[ -f /Users/matthewce/tmp/bb-helper/node_modules/tabtab/.completions/sls.zsh ]] && . /Users/matthewce/tmp/bb-helper/node_modules/tabtab/.completions/sls.zsh
# tabtab source for slss package
# uninstall by removing these lines or running `tabtab uninstall slss`
[[ -f /Users/matthewce/tmp/bb-helper/node_modules/tabtab/.completions/slss.zsh ]] && . /Users/matthewce/tmp/bb-helper/node_modules/tabtab/.completions/slss.zsh
